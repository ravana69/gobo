<!DOCTYPE html>
<html>

<!-- Mirrored from ikeryou.jp/works/gobo/ by HTTrack Website Copier/3.x [XR&CO'2014], Tue, 24 May 2022 11:28:31 GMT -->
<head>
  <title>GOBOGOBO</title>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0, maximum-scale=1.0, user-scalable=no">
  <meta name="keywords" content="webgl,javascript,glsl,noise,glitch">
  <meta name="description" content="">
  <meta property="og:description" content="" />
  <meta property="og:type" content="website" />
  <meta property="og:url" content="index.html" />
  <meta property="og:image" content="assets/img/ogp/ogp.png" />
  <meta property="og:site_name" content="GOBOGOBO" />
  <meta property="og:title" content="GOBOGOBO" />
  <meta name="twitter:card" content="summary_large_image">
  <meta name="twitter:title" content="GOBOGOBO">
  <meta name="twitter:description" content="">
  <meta name="twitter:image" content="assets/img/ogp/ogp.png">
  <link rel="stylesheet" href="assets/css/html5reset-1.6.1.css">
  
  
  <script id="vLine" type="x-shader/x-vertex">
    
    attribute vec3 position;
    attribute vec3 range;
    attribute float id;
    
    uniform mat4 mvpMatrix;
    uniform float time;
    uniform float rangeOffset;
    
    void main(void) {
      
      vec3 p = position + sin(id + time * 2.0) * range * rangeOffset;
      gl_Position = mvpMatrix * vec4(p, 1.0);
      
    }
    
  </script>
  <script id="fLine" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform vec4 baseColor;
    
    void main(void) {
      
      vec4 destColor = baseColor;
      gl_FragColor = destColor;
      
    }
    
  </script>
  
  
  <script id="vDot" type="x-shader/x-vertex">
    
    attribute vec3 position;
    attribute vec3 normal;
    
    uniform float id;
    uniform float range;
    uniform mat4 mvpMatrix;
    uniform float time;
    
    varying vec3 vNormal;
    
    void main(void) {
      
      vec3 p = position + sin(id + time * 2.0) * range * normal;
      gl_Position = mvpMatrix * vec4(p, 1.0);
      
    }
    
  </script>
  <script id="fDot" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform vec4 baseColor;
    
    void main(void) {
      
      gl_FragColor = baseColor;
      
    }
    
    
  </script>
  
  <script id="vGaussian" type="x-shader/x-vertex">
    
    attribute vec3 position;
    attribute vec2 texCoord;
    
    uniform mat4 mvpMatrix;
    
    varying vec2 vTexCoord;
    
    void main(void) {
      
      vTexCoord = texCoord;
      gl_Position = mvpMatrix * vec4(position, 1.0);
      
    }
    
  </script>
  <script id="fGaussian" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform sampler2D texture;
    uniform float     weight[10];
    uniform bool      horizontal;
    uniform vec2      resolution;
    
    varying vec2      vTexCoord;
    
    void main(void) {
      
      vec2 tFrag = 1.0 / resolution;
      vec2 fc;
      vec3 destColor = vec3(0.0);
      
      if(horizontal){
          fc = vec2(gl_FragCoord.s, resolution.t - gl_FragCoord.t);
          destColor += texture2D(texture, (fc + vec2(-9.0, 0.0)) * tFrag).rgb * weight[9];
          destColor += texture2D(texture, (fc + vec2(-8.0, 0.0)) * tFrag).rgb * weight[8];
          destColor += texture2D(texture, (fc + vec2(-7.0, 0.0)) * tFrag).rgb * weight[7];
          destColor += texture2D(texture, (fc + vec2(-6.0, 0.0)) * tFrag).rgb * weight[6];
          destColor += texture2D(texture, (fc + vec2(-5.0, 0.0)) * tFrag).rgb * weight[5];
          destColor += texture2D(texture, (fc + vec2(-4.0, 0.0)) * tFrag).rgb * weight[4];
          destColor += texture2D(texture, (fc + vec2(-3.0, 0.0)) * tFrag).rgb * weight[3];
          destColor += texture2D(texture, (fc + vec2(-2.0, 0.0)) * tFrag).rgb * weight[2];
          destColor += texture2D(texture, (fc + vec2(-1.0, 0.0)) * tFrag).rgb * weight[1];
          destColor += texture2D(texture, (fc + vec2( 0.0, 0.0)) * tFrag).rgb * weight[0];
          destColor += texture2D(texture, (fc + vec2( 1.0, 0.0)) * tFrag).rgb * weight[1];
          destColor += texture2D(texture, (fc + vec2( 2.0, 0.0)) * tFrag).rgb * weight[2];
          destColor += texture2D(texture, (fc + vec2( 3.0, 0.0)) * tFrag).rgb * weight[3];
          destColor += texture2D(texture, (fc + vec2( 4.0, 0.0)) * tFrag).rgb * weight[4];
          destColor += texture2D(texture, (fc + vec2( 5.0, 0.0)) * tFrag).rgb * weight[5];
          destColor += texture2D(texture, (fc + vec2( 6.0, 0.0)) * tFrag).rgb * weight[6];
          destColor += texture2D(texture, (fc + vec2( 7.0, 0.0)) * tFrag).rgb * weight[7];
          destColor += texture2D(texture, (fc + vec2( 8.0, 0.0)) * tFrag).rgb * weight[8];
          destColor += texture2D(texture, (fc + vec2( 9.0, 0.0)) * tFrag).rgb * weight[9];
      }else{
          fc = gl_FragCoord.st;
          destColor += texture2D(texture, (fc + vec2(0.0, -9.0)) * tFrag).rgb * weight[9];
          destColor += texture2D(texture, (fc + vec2(0.0, -8.0)) * tFrag).rgb * weight[8];
          destColor += texture2D(texture, (fc + vec2(0.0, -7.0)) * tFrag).rgb * weight[7];
          destColor += texture2D(texture, (fc + vec2(0.0, -6.0)) * tFrag).rgb * weight[6];
          destColor += texture2D(texture, (fc + vec2(0.0, -5.0)) * tFrag).rgb * weight[5];
          destColor += texture2D(texture, (fc + vec2(0.0, -4.0)) * tFrag).rgb * weight[4];
          destColor += texture2D(texture, (fc + vec2(0.0, -3.0)) * tFrag).rgb * weight[3];
          destColor += texture2D(texture, (fc + vec2(0.0, -2.0)) * tFrag).rgb * weight[2];
          destColor += texture2D(texture, (fc + vec2(0.0, -1.0)) * tFrag).rgb * weight[1];
          destColor += texture2D(texture, (fc + vec2(0.0,  0.0)) * tFrag).rgb * weight[0];
          destColor += texture2D(texture, (fc + vec2(0.0,  1.0)) * tFrag).rgb * weight[1];
          destColor += texture2D(texture, (fc + vec2(0.0,  2.0)) * tFrag).rgb * weight[2];
          destColor += texture2D(texture, (fc + vec2(0.0,  3.0)) * tFrag).rgb * weight[3];
          destColor += texture2D(texture, (fc + vec2(0.0,  4.0)) * tFrag).rgb * weight[4];
          destColor += texture2D(texture, (fc + vec2(0.0,  5.0)) * tFrag).rgb * weight[5];
          destColor += texture2D(texture, (fc + vec2(0.0,  6.0)) * tFrag).rgb * weight[6];
          destColor += texture2D(texture, (fc + vec2(0.0,  7.0)) * tFrag).rgb * weight[7];
          destColor += texture2D(texture, (fc + vec2(0.0,  8.0)) * tFrag).rgb * weight[8];
          destColor += texture2D(texture, (fc + vec2(0.0,  9.0)) * tFrag).rgb * weight[9];
      }
      
      gl_FragColor = vec4(destColor, 1.0);
    }
    
    
  </script>
  
  
  
  <script id="vBlur" type="x-shader/x-vertex">
    
    attribute vec3 position;
    uniform mat4 mvpMatrix;
    
    void main(void) {
      
      gl_Position = mvpMatrix * vec4(position, 1.0);
      
    }
    
  </script>
  <script id="fBlur" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform sampler2D texture;
    uniform vec2 resolution;
    
    void main(void) {
      
      vec2 tFrag = 1.0 / resolution;
      vec4 destColor = texture2D(texture, gl_FragCoord.st * tFrag);
    
      destColor *= 0.36;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-1.0,  1.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 0.0,  1.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 1.0,  1.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-1.0,  0.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 1.0,  0.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-1.0, -1.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 0.0, -1.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 1.0, -1.0)) * tFrag) * 0.04;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-2.0,  2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-1.0,  2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 0.0,  2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 1.0,  2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 2.0,  2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-2.0,  1.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 2.0,  1.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-2.0,  0.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 2.0,  0.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-2.0, -1.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 2.0, -1.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-2.0, -2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2(-1.0, -2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 0.0, -2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 1.0, -2.0)) * tFrag) * 0.02;
      destColor += texture2D(texture, (gl_FragCoord.st + vec2( 2.0, -2.0)) * tFrag) * 0.02;
  
      gl_FragColor = destColor;
    
    }
    
    
  </script>
  
  
  <script id="vNoise" type="x-shader/x-vertex">
    
    attribute vec3 position;
    
    void main(void) {
      gl_Position = vec4(position, 1.0);
    }
    
  </script>
  <script id="fNoise" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform vec2 resolution;
    uniform vec2 offset;
    
    const int   oct      = 8;
    const float per      = 0.5;
    const float PI       = 3.1415926;
    const float cCorners = 1.0 / 16.0;
    const float cSides   = 1.0 / 8.0;
    const float cCenter  = 1.0 / 4.0;
    
    // 補間関数
    float interpolate(float a, float b, float x) {
      float f = (1.0 - cos(x * PI)) * 0.5;
      return a * (1.0 - f) + b * f;
    }
    
    // 乱数生成
    float rnd(vec2 p) {
      return fract(sin(dot(p, vec2(12.9898,78.233))) * 43758.5453);
    }
    
    // 補間乱数
    float irnd(vec2 p) {
      vec2 i = floor(p);
      vec2 f = fract(p);
      vec4 v = vec4(
        rnd(vec2(i.x, i.y)),
        rnd(vec2(i.x + 1.0, i.y)),
        rnd(vec2(i.x, i.y + 1.0)),
        rnd(vec2(i.x + 1.0, i.y + 1.0))
      );
      return interpolate(interpolate(v.x, v.y, f.x), interpolate(v.z, v.w, f.x), f.y);
    }
    
    // ノイズ生成
    float noise(vec2 p) {
      float t = 0.0;
      for(int i = 0; i < oct; i++) {
        float freq = pow(2.0, float(i));
        float amp = pow(per, float(oct - i));
        t += irnd(vec2(p.x / freq, p.y / freq)) * amp;
      }
      return t;
    }
    
    
    // HSVカラー生成
    vec3 hsv(float h, float s, float v) {
      vec4 t = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
      vec3 p = abs(fract(vec3(h) + t.xyz) * 6.0 - vec3(t.w));
      return v * mix(vec3(t.x), clamp(p - vec3(t.x), 0.0, 1.0), s);
    }
    
    void main(void) {
      
      float n = noise(gl_FragCoord.xy + offset);
      gl_FragColor = vec4(vec3(n), 1.0);
      
    }
    
  </script>
  
  <script id="vColorCorrection" type="x-shader/x-vertex">
    
    attribute vec3 position;
    uniform mat4 mvpMatrix;
    
    void main(void) {
      
      gl_Position = mvpMatrix * vec4(position, 1.0);
      
    }
    
  </script>
  <script id="fColorCorrection" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform sampler2D texture;
    uniform vec2 resolution;
    uniform vec3 powRGB;
    uniform vec3 mulRGB;
    
    void main(void) {
      
      vec2 tFrag = 1.0 / resolution;
      vec4 destColor = texture2D(texture, gl_FragCoord.st * tFrag);
      gl_FragColor = vec4(mulRGB * pow(destColor.rgb, powRGB), destColor.a);
    
    }
  
  </script>
  
  <script id="vFXAA" type="x-shader/x-vertex">
    
    attribute vec3 position;
    uniform mat4 mvpMatrix;
    
    void main(void) {
      
      gl_Position = mvpMatrix * vec4(position, 1.0);
      
    }
    
  </script>
  <script id="fFXAA" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform sampler2D texture;
    uniform vec2 resolution;
    uniform float reduceMin;
    uniform float reduceMul;
    uniform float spanMax;
    
    void main(void) {
      
      vec3 rgbNW = texture2D( texture, ( gl_FragCoord.xy + vec2( -1.0, -1.0 ) ) * resolution ).xyz;
      vec3 rgbNE = texture2D( texture, ( gl_FragCoord.xy + vec2( 1.0, -1.0 ) ) * resolution ).xyz;
      vec3 rgbSW = texture2D( texture, ( gl_FragCoord.xy + vec2( -1.0, 1.0 ) ) * resolution ).xyz;
      vec3 rgbSE = texture2D( texture, ( gl_FragCoord.xy + vec2( 1.0, 1.0 ) ) * resolution ).xyz;
      vec4 rgbaM  = texture2D( texture,  gl_FragCoord.xy  * resolution );
      vec3 rgbM  = rgbaM.xyz;
      vec3 luma = vec3( 0.299, 0.587, 0.114 );
      
      float lumaNW = dot( rgbNW, luma );
      float lumaNE = dot( rgbNE, luma );
      float lumaSW = dot( rgbSW, luma );
      float lumaSE = dot( rgbSE, luma );
      float lumaM  = dot( rgbM,  luma );
      float lumaMin = min( lumaM, min( min( lumaNW, lumaNE ), min( lumaSW, lumaSE ) ) );
      float lumaMax = max( lumaM, max( max( lumaNW, lumaNE),  max( lumaSW, lumaSE ) ) );
      
      vec2 dir;
      dir.x = -((lumaNW + lumaNE) - (lumaSW + lumaSE));
      dir.y =  ((lumaNW + lumaSW) - (lumaNE + lumaSE));
      
      float dirReduce = max( ( lumaNW + lumaNE + lumaSW + lumaSE ) * ( 0.25 * reduceMul ), reduceMin );
      
      float rcpDirMin = 1.0 / ( min( abs( dir.x ), abs( dir.y ) ) + dirReduce );
      dir = min( vec2( spanMax, spanMax), max( vec2(-spanMax, -spanMax), dir * rcpDirMin)) * resolution;
      vec4 rgbA = (1.0/2.0) * (
        texture2D(texture,  gl_FragCoord.xy  * resolution + dir * (1.0/3.0 - 0.5)) +
        texture2D(texture,  gl_FragCoord.xy  * resolution + dir * (2.0/3.0 - 0.5)));
      vec4 rgbB = rgbA * (1.0/2.0) + (1.0/4.0) * (
        texture2D(texture,  gl_FragCoord.xy  * resolution + dir * (0.0/3.0 - 0.5)) +
        texture2D(texture, gl_FragCoord.xy  * resolution + dir * (3.0/3.0 - 0.5)));
      
      float lumaB = dot(rgbB, vec4(luma, 0.0));
      
      if ( ( lumaB < lumaMin ) || ( lumaB > lumaMax ) ) {
        gl_FragColor = rgbA;
      } else {
        gl_FragColor = rgbB;
      }
      
    }
    
  </script>
  
  
  
  
  
  <script id="vPartialDraw" type="x-shader/x-vertex">
    
    attribute vec3 position;
    attribute vec2 texCoord;
    
    uniform mat4 mvpMatrix;
    
    varying vec2 vTexCoord;
    
    void main(void) {
      
      vTexCoord = texCoord;
      gl_Position = mvpMatrix * vec4(position, 1.0);
      
    }
    
  </script>
  <script id="fPartialDraw" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform sampler2D texture0;
    uniform sampler2D texture1;
    uniform sampler2D texture2;
    
    // ノイズ用
    uniform sampler2D texture3;
    
    uniform float speed[3];
    uniform float offset[3];
    uniform float alpha;
    
    varying vec2 vTexCoord;
    
    vec4 addColor(vec4 cA, vec4 cB) {
      return vec4((cA.rgb * vec3(cA.a)) + (cB.rgb * vec3(1.0 - cA.a)), cA.a + cB.a);
    }
    
    void main(void) {
      
      vec4 noise = texture2D(texture3, vTexCoord);
      float k = 0.5;
      
      vec4 color0 = texture2D(texture0, vTexCoord);
      color0.a *= (sin(noise.r * offset[0] + speed[0]) + 1.0) * k;
      
      vec4 color1 = texture2D(texture1, vTexCoord);
      color1.a *= (sin(noise.r * offset[1] + speed[1]) + 1.0) * k;
      
      vec4 color2 = texture2D(texture2, vTexCoord);
      color2.a *= (sin(noise.r * offset[2] + speed[2]) + 1.0) * k;
      
      vec4 destColor = addColor(color2, addColor(color0, color1));
      destColor.a *= alpha;
      
      gl_FragColor = destColor;
      
    }
    
  </script>
  
  <script id="vGlitch" type="x-shader/x-vertex">
    
    attribute vec3 position;
    uniform mat4 mvpMatrix;
    
    void main(void) {
      
      gl_Position = mvpMatrix * vec4(position, 1.0);
      
    }
    
  </script>
  <script id="fGlitch" type="x-shader/x-fragment">
    
    precision mediump float;
    
    uniform sampler2D texture;
    uniform sampler2D noiseTexture;
    uniform vec2 resolution;
    uniform vec3 distortion;
    
    vec4 addColor(vec4 cA, vec4 cB) {
      return vec4((cA.rgb * vec3(cA.a)) + (cB.rgb * vec3(1.0 - cA.a)), cA.a + cB.a);
    }
    
    void main(void) {
      
      vec2 tFrag = 1.0 / resolution;
      vec4 noise = texture2D(noiseTexture, gl_FragCoord.st * tFrag);
      
      vec2 offsetR = vec2(noise.r * distortion.r, noise.r * distortion.r);
      vec2 offsetG = vec2(noise.r * distortion.g, noise.r * distortion.g);
      vec2 offsetB = vec2(noise.r * distortion.b, noise.r * distortion.b);
      
      vec4 rColor    = texture2D(texture, fract(gl_FragCoord.st * tFrag + offsetR));
      vec4 gColor    = texture2D(texture, fract(gl_FragCoord.st * tFrag + offsetG));
      vec4 bColor    = texture2D(texture, fract(gl_FragCoord.st * tFrag + offsetB));
      
      gl_FragColor = vec4(rColor.r, gColor.g, bColor.b, 1.0);
    
    }
  
  </script>
  
</head>
<body>
  <canvas id="xBg"></canvas>
  <script src="assets/js/libs.js"></script>
  <script src="assets/js/main.js"></script>
</body>

<!-- Mirrored from ikeryou.jp/works/gobo/ by HTTrack Website Copier/3.x [XR&CO'2014], Tue, 24 May 2022 11:28:37 GMT -->
</html>
















